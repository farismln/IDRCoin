# [H-02] The precision loss in the `IDRCoin::convertUSDtoIDR` function led to receiving fewer IDR than expected.

## Submitter

ZafiN

## Finding description and impact

The issue in the implementation of the USD to IDR conversion in the `convertUSDtoIDR` function, caused by a precision loss bug, led to a user who converted USD to IDR receiving less than they should have. Generally speaking, if n is an integer, then for all values of a$ being converted, where a is within the range [n, n+1), the resulting IDR would be the same.

## Proof of Concept


```js
    // external/public function
    // anyone can buy IDRC with USDT with fixed conversion rate
    function convertUSDtoIDR(uint256 amountInUSD) external {
        usdt.transferFrom(msg.sender, address(this), amountInUSD);
        // first we normalize the amount in usd by dividing it with its own decimals
        // then we multiply it with the conversion rate and IDRC decimals
        // result is the amount of IDRC to mint with the correct decimals
        uint256 amountInIDR = (amountInUSD / 10 ** usdt.decimals()) *CONVERSION_RATE *  10 ** decimals();
        mint_(msg.sender, amountInIDR);

        emit IDRC_Mint(msg.sender, amountInIDR);
    }
```

Based on the `convertUSDtoIDR` function, there is an implementation error in the multiplication after division, which causes precision loss. This results in the conversion producing less IDR than expected for a given amount of USD. For example, in Solidity, (4 * 5) / 10 results in 2, while 4 * (5 / 10) results in 0. This difference occurs due to the precision loss when performing multiplication after division.

To verify this findings, add the following test to `Base.t.sol`:

```js
    function test_precisionLossWhenConvertingToIDRFromUSD() public {
        address user = makeAddr(""user"");
        
        // mint 1000$ USDT to user
        usdtMock.mint(user, 1000e6);


        uint256 tax = idrCoin.TAX();
        uint256 proportionAfterTax = 100 - tax;
        uint256 denominator = idrCoin.DENOMINATOR();
        uint256 conversionRate = idrCoin.CONVERSION_RATE();

        uint256 numUSDtoConvert = 6.99e6;
        uint256 expectedIDRToGet = (numUSDtoConvert * conversionRate * (10 ** idrCoin.decimals()) * proportionAfterTax) / (10 ** usdtMock.decimals()) / denominator;

        vm.startPrank(user);
        
        usdtMock.approve(address(idrCoin), numUSDtoConvert);
        idrCoin.convertUSDtoIDR(numUSDtoConvert);
        
        // assert
        assertEq(expectedIDRToGet, idrCoin.balanceOf(user)); // user get le
        
        vm.stopPrank();
    }
```

then run `forge test --mt test_precisionLossWhenConvertingToIDRFromUSD -v`. the test would FAILED:

```bash
[⠒] Compiling...
No files changed, compilation skipped

Ran 1 test for test/Base.t.sol:Base
[FAIL: assertion failed: 98419200000000000000000 != 84480000000000000000000] test_precisionLossWhenConvertingToIDRFromUSD() (gas: 151335)
Suite result: FAILED. 0 passed; 1 failed; 0 skipped; finished in 8.18ms (755.58µs CPU time)

Ran 1 test suite in 50.81ms (8.18ms CPU time): 0 tests passed, 1 failed, 0 skipped (1 total tests)

Failing tests:
Encountered 1 failing test in test/Base.t.sol:Base
[FAIL: assertion failed: 98419200000000000000000 != 84480000000000000000000] test_precisionLossWhenConvertingToIDRFromUSD() (gas: 151335)

Encountered a total of 1 failing tests, 0 tests succeeded
```

## Recommended mitigation steps

Modify the implementation of the `convertUSDtoIDR` function to perform division after multiplication, as shown in the code below:

```diff
diff --git a/src/IDRCoin.sol b/src/IDRCoin.sol
index c9b6d40..8b7715e 100644
--- a/src/IDRCoin.sol
+++ b/src/IDRCoin.sol

@@ -115,7 +116,11 @@ contract IDRCoin is ERC20{
    // external/public function
    // anyone can buy IDRC with USDT with fixed conversion rate
    function convertUSDtoIDR(uint256 amountInUSD) external {
        usdt.transferFrom(msg.sender, address(this), amountInUSD);
        // first we normalize the amount in usd by dividing it with its own decimals
        // then we multiply it with the conversion rate and IDRC decimals
        // result is the amount of IDRC to mint with the correct decimals
-        uint256 amountInIDR = (amountInUSD / 10 ** usdt.decimals()) *CONVERSION_RATE *  10 ** decimals();
+        uint256 amountInIDR = (amountInUSD * CONVERSION_RATE *  10 ** decimals()) / 10 ** usdt.decimals();
        mint_(msg.sender, amountInIDR);

        emit IDRC_Mint(msg.sender, amountInIDR);
    }
     }

```
